#! /bin/csh -f

# Modifications: $Log: mincheader,v $
# Modifications: Revision 3.0  1995-05-15 19:31:32  neelin
# Modifications: Release of minc version 0.3
# Modifications:
# Revision 2.2  1995/01/25  08:04:41  neelin
# Added check for failure to read minc file.
#
# Revision 2.1  95/01/24  10:19:09  neelin
# Added support for compressed files.
# 
# Revision 2.0  94/09/28  10:34:10  neelin
# Release of minc version 0.2
# 
# Revision 1.6  94/09/28  10:34:06  neelin
# Pre-release
# 
# Revision 1.5  93/10/14  10:09:35  neelin
# Fixed usage error message.
# 
# Revision 1.4  93/08/25  11:24:20  neelin
# Added checking for -h or -help options.
# 
# Revision 1.3  93/08/11  15:20:55  neelin
# Added RCS logging in source.
# 
#

# Set defaults
if (! $?TMPDIR) set TMPDIR = /usr/tmp
set header_opts = "-header_only"
set header_only = 1

# Check arguments
set usage = "Usage: $0 [-data] <minc file>"
if (($#argv < 1) || ($#argv > 2)) then
   echo "$usage"
   exit -1
endif
if ($#argv == 1) then
   if (("$1" == "-help") || ("$1" == "-h") || ("$1" == "")) then
      echo "$usage"
      exit -1
   endif
else
   if ("$1" != "-data") then
      echo "$usage"
      exit -1
   endif
   set header_opts = "-all_data"
   set header_only = 0
   shift
endif
set file = $1

# Set interrupt handling
onintr cleanup

# Expand file if needed
set name = $file:t
set tempfile = $TMPDIR/mincheader-$$-$name:r
set dumpfile = `mincexpand $file $tempfile -name_only $header_opts`

# Dump header only for compressed file
if (("$dumpfile" == "$tempfile") && ($header_only)) then
   ncdump -h $dumpfile

# Dump everything if we've got it
else

#  Get variables names (excluding image): remove leading "image", remove word 
#  "image", collapse multiple blanks to one, remove leading blanks, remove
#  trailing blanks, replace blanks with commas
   set vars = `mincinfo -varnames $dumpfile | sed -e 's/^image //g' -e 's/ image / /g' -e 's/  */ /g' -e 's/^ //' -e 's/ $//' -e 's/ /,/g'`
   if ($vars != "") then
      ncdump -v "$vars" $dumpfile
   endif
endif


cleanup:
   rm -f $tempfile
