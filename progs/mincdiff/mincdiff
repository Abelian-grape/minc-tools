#! /bin/csh -f
#
# Script to find differences between minc files
#
# Usage: mincdiff [-l] <file1.mnc> <file2.mnc>
#
# Modifications: 
#   $Log: mincdiff,v $
#   Revision 6.1  1999-10-19 14:45:20  neelin
#   Fixed Log subsitutions for CVS
#
#   Revision 6.0  1997/09/12 13:23:46  neelin
#   Release of minc version 0.6
#   
#   Revision 5.0  1997/08/21  13:24:47  neelin
#   Release of minc version 0.5
#  
#   Revision 4.0  1997/05/07  20:01:01  neelin
#   Release of minc version 0.4
#  
#   Revision 3.0  1995/05/15  19:31:40  neelin
#   Release of minc version 0.3
#  
#   Revision 2.1  1995/01/24  09:12:29  neelin
#   Added support for compressed minc files.
#  
#   Revision 2.0  94/09/28  10:34:46  neelin
#   Release of minc version 0.2
#   
#   Revision 1.4  94/09/28  10:34:44  neelin
#   Pre-release
#   
#   Revision 1.3  94/07/18  10:41:26  neelin
#   Added -nonormalize option to mincextract for comparing image data.
#   
#   Revision 1.2  93/11/01  16:50:03  neelin
#   Some fixes.
# 
#
# Copyright 1993 Peter Neelin, McConnell Brain Imaging Centre, 
# Montreal Neurological Institute, McGill University.
# Permission to use, copy, modify, and distribute this
# software and its documentation for any purpose and without
# fee is hereby granted, provided that the above copyright
# notice appear in all copies.  The author and McGill University
# make no representations about the suitability of this
# software for any purpose.  It is provided "as is" without
# express or implied warranty.

# Constants
if (! $?TMPDIR) set TMPDIR = /usr/tmp
set working_dir = $TMPDIR

# Check arguments
switch ("$#argv")
case 2:
   set file1 = $1
   set file2 = $2
   set cmp_switch = ""
   breaksw
case 3:
   if ("$1" == "-l") then
      set file1 = $2
      set file2 = $3
      set cmp_switch = -l
      breaksw
   endif
default:
   echo "Usage: $0 [-l] <file1.mnc> <file2.mnc>"
   exit
endsw

# Set up the file names
set file1base = $file1:r
set file2base = $file2:r
set fileprefix = $working_dir/mincdiff-$$
set file1tmp  = ${fileprefix}-1-${file1base:t}
set file2tmp  = ${fileprefix}-2-${file2base:t}
set file1tmpmnc = ${fileprefix}-minc-1-${file1base:t}
set file2tmpmnc = ${fileprefix}-minc-2-${file1base:t}

# Set interrupt handling
onintr cleanup

# Expand the files
set dumpfile1 = `mincexpand $file1 $file1tmp.mnc -name_only`
set dumpfile2 = `mincexpand $file2 $file2tmp.mnc -name_only`

# Dump the headers and compare them
mincheader $dumpfile1 > $file1tmp.cdl && \
   mincheader $dumpfile2 > $file2tmp.cdl
if ($status) then
   echo "${0}: Error reading file headers"
   goto cleanup
endif
diff $file1tmp.cdl $file2tmp.cdl
if ($status) then
   echo "Binary image comparison:"
endif

# Dump the raw data and compare them
mincextract -nonormalize -file $dumpfile1 > $file1tmp.raw && \
      mincextract -nonormalize -file $dumpfile2 > $file2tmp.raw
if ($status) then
   echo "${0}: Error reading raw data"
   goto cleanup
endif
cmp $cmp_switch $file1tmp.raw $file2tmp.raw

cleanup:
   rm -f {$file1tmp,$file2tmp}.{raw,cdl,mnc}
   exit

